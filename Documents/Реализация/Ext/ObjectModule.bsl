
//списание товаров по ФИФО
Процедура ОбработкаПроведения(Отказ, Режим)

    Движения.ОстаткиТоваров.Записывать = Истина;
	Движения.СтоимостьТоваров.Записывать = Истина;
	Движения.Продажи.Записывать = Истина;
	Движения.ОстаткиТоваровПоПартиям.Записывать = Истина;

	// Менеджер временных таблиц
	МенеджерВТ = Новый МенеджерВременныхТаблиц;

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;

	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РеализацияТовары.Номенклатура КАК Номенклатура,
		|	СУММА(РеализацияТовары.Количество) КАК КоличествоВДокументе,
		|	СУММА(РеализацияТовары.Сумма) КАК СуммаВДокументе
		|ПОМЕСТИТЬ НоменклатураДокумента
		|ИЗ
		|	Документ.Реализация.Товары КАК РеализацияТовары
		|ГДЕ
		|	РеализацияТовары.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	РеализацияТовары.Номенклатура";

	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	РезультатЗапроса = Запрос.Выполнить();
               
	Запрос2 = Новый Запрос;
	Запрос2.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос2.Текст = "ВЫБРАТЬ
				|	НоменклатураДокумента.Номенклатура КАК Номенклатура,
				|	НоменклатураДокумента.КоличествоВДокументе КАК КоличествоВДокументе,
				|	НоменклатураДокумента.СуммаВДокументе КАК СуммаВДокументе,
				|	ЕСТЬNULL(ОстаткиТоваровПоПартиямОстатки.КоличествоОстаток, 0) КАК КоличествоВПартии,
				|	ЕСТЬNULL(ОстаткиТоваровПоПартиямОстатки.СтоимостьОстаток, 0) КАК СтоимостьВПартии,
				|	ОстаткиТоваровПоПартиямОстатки.Партия КАК Партия,
				|	ОстаткиТоваровПоПартиямОстатки.Партия.МоментВремени КАК МоментВремени
				|ИЗ
				|	НоменклатураДокумента КАК НоменклатураДокумента
				|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиТоваровПоПартиям.Остатки(
				|				&МоментВремени,
				|				Товар В
				|					(ВЫБРАТЬ
				|						НоменклатураДокумента.Номенклатура
				|					ИЗ
				|						НоменклатураДокумента)) КАК ОстаткиТоваровПоПартиямОстатки
				|		ПО НоменклатураДокумента.Номенклатура = ОстаткиТоваровПоПартиямОстатки.Товар
				|
				|УПОРЯДОЧИТЬ ПО
				|	Номенклатура,
				|	МоментВремени";

	
	// Установим необходимость блокировки данных в регистре ОстаткиТоваровПоПартиям 
    Движения.ОстаткиТоваровПоПартиям.БлокироватьДляИзменения = Истина;
    // Запишем пустые наборы записей, чтобы читать остатки без учета данных в документе
    Движения.ОстаткиТоваровПоПартиям.Записать();

   	Запрос2.УстановитьПараметр("МоментВремени", МоментВремени());
    РезультатЗапроса = Запрос2.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ТекущаяНоменклатура = Неопределено;  // Текущая обрабатываемая номенклатура
	КоличествоОсталось = 0;            // Остаток к списанию для текущей номенклатуры

	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		// Если номенклатура изменилась — начинаем новую обработку
		Если ВыборкаДетальныеЗаписи.Номенклатура <> ТекущаяНоменклатура Тогда   
			
			//// Проверяем, не осталось ли не списанного количества для предыдущей номенклатуры
			Если ТекущаяНоменклатура <> Неопределено И КоличествоОсталось > 0 Тогда
				Отказ = Истина;
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = "Недостаточно остатков для списания по номенклатуре """ + ТекущаяНоменклатура + """ в количестве " + КоличествоОсталось + " шт." ;
				Сообщение.Сообщить();
			КонецЕсли;
            			                       		
			// Инициализируем новую номенклатуру
			ТекущаяНоменклатура = ВыборкаДетальныеЗаписи.Номенклатура;
			КоличествоОсталось = ВыборкаДетальныеЗаписи.КоличествоВДокументе;
		КонецЕсли;

		//// Если уже всё списали для этой номенклатуры — пропускаем
		Если КоличествоОсталось <= 0 Тогда
			Продолжить;
		КонецЕсли;

		// Списываем из текущей партии
		КоличествоДляСписания = Мин(КоличествоОсталось, ВыборкаДетальныеЗаписи.КоличествоВПартии);

		// Расчёт суммы списания
		Если ВыборкаДетальныеЗаписи.КоличествоВПартии > 0 Тогда
			СуммаДляСписания = КоличествоДляСписания *
				(ВыборкаДетальныеЗаписи.СтоимостьВПартии / ВыборкаДетальныеЗаписи.КоличествоВПартии);
		Иначе
			СуммаДляСписания = 0;
		КонецЕсли;

		
		// регистр ОстаткиТоваров Расход
		Движение = Движения.ОстаткиТоваров.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Товар = ВыборкаДетальныеЗаписи.Номенклатура;
		Движение.Количество = КоличествоДляСписания;

		// регистр СтоимостьТоваров Расход
		Движение = Движения.СтоимостьТоваров.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Товар = ВыборкаДетальныеЗаписи.Номенклатура;
		Движение.Стоимость = СуммаДляСписания;

		// регистр Продажи
		Движение = Движения.Продажи.Добавить();
		Движение.Период = Дата;
		Движение.Номенклатура = ВыборкаДетальныеЗаписи.Номенклатура;
		Движение.Контрагент = Покупатель;
		Движение.Количество = КоличествоДляСписания;
		Движение.Выручка = (КоличествоДляСписания / ВыборкаДетальныеЗаписи.КоличествоВДокументе) *
			ВыборкаДетальныеЗаписи.СуммаВДокументе;
		Движение.Стоимость = СуммаДляСписания;

		// регистр ОстаткиТоваровПоПартиям (Расход)
		Движение = Движения.ОстаткиТоваровПоПартиям.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Товар = ВыборкаДетальныеЗаписи.Номенклатура;
		Движение.Партия = ВыборкаДетальныеЗаписи.Партия;
		Движение.Количество = КоличествоДляСписания;
		Движение.Стоимость = СуммаДляСписания;

		// Уменьшаем остаток к списанию
		КоличествоОсталось = КоличествоОсталось - КоличествоДляСписания;

	КонецЦикла;
	
	    // Проверка на нехватку для последней номенклатуры
	Если ТекущаяНоменклатура <> Неопределено И КоличествоОсталось > 0 Тогда
		Отказ = Истина;
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Недостаточно остатков для списания по номенклатуре """ + ТекущаяНоменклатура + """ в количестве " + КоличествоОсталось + " шт." ;

		Сообщение.Сообщить();
	КонецЕсли;

	   
КонецПроцедуры   

#Область СписаниеПоСреднейСтоимости
// Процедура ОбработкаПроведения(Отказ, Режим)
//	
//		
//	Движения.ОстаткиТоваров.Записывать = Истина;
//	Движения.СтоимостьТоваров.Записывать = Истина;
//    Движения.Продажи.Записывать = Истина;
//	Движения.ОстаткиТоваровПоПартиям.Записывать = Истина; 
//	
//	
//	// Создать менеджер временных таблиц
//    МенеджерВТ = Новый МенеджерВременныхТаблиц;

//    Запрос = Новый Запрос;

//    // Укажем, какой менеджер временных таблиц использует этот запрос
//    Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
//		  	
//	Запрос.Текст = 
//		"ВЫБРАТЬ
//		|	РеализацияТовары.Номенклатура КАК Номенклатура,
//		|	СУММА(РеализацияТовары.Количество) КАК КоличествоВДокументе,
//		|	СУММА(РеализацияТовары.Сумма) КАК СуммаВДокументе
//		|ПОМЕСТИТЬ НоменклатураДокумента
//		|ИЗ
//		|	Документ.Реализация.Товары КАК РеализацияТовары
//		|ГДЕ
//		|	РеализацияТовары.Ссылка = &Ссылка
//		|
//		|СГРУППИРОВАТЬ ПО
//		|	РеализацияТовары.Номенклатура";
//	
//	Запрос.УстановитьПараметр("Ссылка", Ссылка);
//	
//	РезультатЗапроса = Запрос.Выполнить(); 
//	
//	
//	Запрос2 = Новый Запрос;
//    Запрос2.МенеджерВременныхТаблиц = МенеджерВТ;
//    Запрос2.Текст = "ВЫБРАТЬ
//                    |	НоменклатураДокумента.Номенклатура КАК Номенклатура,
//                    |	НоменклатураДокумента.КоличествоВДокументе КАК КоличествоВДокументе,
//                    |	НоменклатураДокумента.СуммаВДокументе КАК СуммаВДокументе,
//                    |	ЕСТЬNULL(ОстаткиТоваровПоПартиямОстатки.КоличествоОстаток, 0) КАК Количество,
//                    |	ЕСТЬNULL(ОстаткиТоваровПоПартиямОстатки.СтоимостьОстаток, 0) КАК Стоимость
//                    |ИЗ
//                    |	НоменклатураДокумента КАК НоменклатураДокумента
//                    |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ОстаткиТоваровПоПартиям.Остатки(
//                    |				,
//                    |				Товар В
//                    |					(ВЫБРАТЬ
//                    |						НоменклатураДокумента.Номенклатура
//                    |					ИЗ
//                    |						НоменклатураДокумента)) КАК ОстаткиТоваровПоПартиямОстатки
//                    |		ПО НоменклатураДокумента.Номенклатура = ОстаткиТоваровПоПартиямОстатки.Товар";
//	
//	
//	Движения.ОстаткиТоваровПоПартиям.Записать(); 

//	РезультатЗапроса = Запрос2.Выполнить();
//	
//	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
//	
//	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл 
//		Если ВыборкаДетальныеЗаписи.Количество = 0 Тогда
//          	СтоимостьМатериала = 0;
//       	Иначе
//           	СтоимостьМатериала = ВыборкаДетальныеЗаписи.Стоимость / ВыборкаДетальныеЗаписи.Количество;
//       	КонецЕсли;

// 	   		
//		// регистр ОстаткиТоваров Расход
//       	Движение = Движения.ОстаткиТоваров.Добавить();
//		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
//		Движение.Период = Дата;
//		Движение.Товар = ВыборкаДетальныеЗаписи.Номенклатура;
//		Движение.Количество = ВыборкаДетальныеЗаписи.КоличествоВДокументе;
//	

//	    // регистр СтоимостьТоваров Расход
//	   	Движение = Движения.СтоимостьТоваров.Добавить();
//		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
//		Движение.Период = Дата;
//		Движение.Товар = ВыборкаДетальныеЗаписи.Номенклатура;
//		Движение.Стоимость = ВыборкаДетальныеЗаписи.КоличествоВДокументе*СтоимостьМатериала;
//	

//	    // регистр Продажи 
//	   	Движение = Движения.Продажи.Добавить();
//		Движение.Период = Дата;
//		Движение.Номенклатура = ВыборкаДетальныеЗаписи.Номенклатура;
//		Движение.Контрагент = Покупатель;
//		Движение.Количество = ВыборкаДетальныеЗаписи.КоличествоВДокументе;
//		Движение.Выручка = ВыборкаДетальныеЗаписи.СуммаВДокументе;
//		Движение.Стоимость = СтоимостьМатериала*ВыборкаДетальныеЗаписи.КоличествоВДокументе;
//	

//	    // регистр ОстаткиТоваровПоПартиям Расход
//		Движение = Движения.ОстаткиТоваровПоПартиям.Добавить();
//		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
//		Движение.Период = Дата;
//		Движение.Товар = ВыборкаДетальныеЗаписи.Номенклатура;
//		Движение.Партия = Ссылка;
//		Движение.Количество = ВыборкаДетальныеЗаписи.КоличествоВДокументе;
//		Движение.Стоимость = СтоимостьМатериала*ВыборкаДетальныеЗаписи.КоличествоВДокументе;
//		
//	КонецЦикла;
//	
//  
//КонецПроцедуры

 #КонецОбласти

	